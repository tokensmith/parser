allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
    version = "0.0.3-SNAPSHOT"
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = "net.tokensmith"
description = """Parser"""

sourceCompatibility = 12
targetCompatibility = 12

buildscript {
    repositories {
        mavenCentral()
    }
}

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "https://oss.sonatype.org/content/repositories/releases" }
    mavenCentral()
    mavenLocal()
}

configurations {
    deployerJars
}

ext {
    log4jVersion = '2.13.3'
    slf4jVersion = '1.7.25'
    junitVersion = '5.3.1'
    mockitoVersion = '2.28.2'
}

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: "${slf4jVersion}"

    testCompile "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testCompile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: "${log4jVersion}"
    testCompile group: 'org.apache.logging.log4j', name: 'log4j-api', version: "${log4jVersion}"
    testCompile group: 'org.apache.logging.log4j', name: 'log4j-core', version: "${log4jVersion}"
}

tasks.test {
    useJUnit()
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'parser'
            from components.java

            pom {
                name = 'parser'
                packaging = 'jar'
                description = 'Parses HTTP inputs to POJOs'
                url = 'https://github.com/tokensmith/parser'

                scm {
                    connection = 'git@github.com:tokensmith/parser.git'
                    developerConnection = 'git@github.com:tokensmith/parser.git'
                    url = 'https://github.com/tokensmith/parser'
                }

                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'https://github.com/tokensmith/parser/blob/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'tmackenzie'
                        name = 'Tom MacKenzie'
                        email = 'tom@tokensmith.net'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            if(!project.ext.properties.containsKey("ossrhUser")) {
                project.ext.ossrhUser = 'wrongUser'
                project.ext.ossrhPassword = 'wrongPassword'
                print "no ossrh user or password"
            }
            credentials {
                username = project.ext.ossrhUser
                password = project.ext.ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
